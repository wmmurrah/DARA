TITLE:         	 Model 3: Two-level model (female defined at both levels);
 DATA:           FILE IS C:\Mplus\3LCFA.dat;
                 Format is 11f8.0,7f8.2;
VARIABLE: 	     Names are orgcode deptid item1 item2 item3 item4 item5 item6 
                 age female deptsize dept_m orgsize empstab orgdemos orgqual  
                 orgprod1 orgprod2;
                 Usevariables are orgcode item1 item2 item3 item4 item5 item6 
                 female orgdemos; 
             	 Cluster = orgcode; 
                 Within = ;
               	 Between = orgdemos;
ANALYSIS:        TYPE = twolevel random;
                 Estimator is MLR;
MODEL:           ! NOTE: Mplus version 7.3 will produce slightly different 
                 ! model results from Mplus 7.2. 
                 %between%
                 OF1 by item1
                 item2(1)
                 item3(2);
                 OF2 by item4
                 item5(3)
                 item6(4);
                 item1@0 item4@0;
                 S2;
                 OF1 OF2 on orgdemos;
                 OF1 on female;
                 %within%
                 F1 by item1
                 item2(1)
                 item3(2);
                 F2 by item4
                 item5(3)
                 item6(4);
                 item6 with item3;
                 S2 |F2 on female;
                 F1 on female;
  OUTPUT:        sampstat tech1;
